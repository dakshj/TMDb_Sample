apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'io.fabric'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId "com.daksh.tmdbsample"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    dexOptions {
        // Skip pre-dexing when running on Travis CI or when disabled via -Dpre-dex=false.
        preDexLibraries = preDexEnabled && !travisBuild
    }

    packagingOptions {
        exclude 'LICENSE.txt'
    }

    lintOptions {
        abortOnError false
    }

    dataBinding {
        enabled = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    signingConfigs {
        debug {
            storeFile file("../buildsystem/debug.jks")
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }
        debug {
            signingConfig signingConfigs.debug
        }
    }

    productFlavors {
        Production_ {
            buildConfigField "boolean", "IS_PRODUCTION", "true"
            buildConfigField 'String', 'BASE_URL', '"http://api.themoviedb.org/3/"'
            buildConfigField 'String', 'API_KEY', '"3f41e7a0addc26031fdb06aa1c4b6bc0"'
        }

        Development_ {
            buildConfigField "boolean", "IS_PRODUCTION", "false"
            buildConfigField 'String', 'BASE_URL', '"http://api.themoviedb.org/3/"'
            buildConfigField 'String', 'API_KEY', '"3f41e7a0addc26031fdb06aa1c4b6bc0"'

            // Appends to the Application ID when using this Product Flavor
            applicationIdSuffix = ".dev"
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    // Support
    compile "com.android.support:support-v4:$rootProject.supportLibraryVersion"
    compile "com.android.support:appcompat-v7:$rootProject.supportLibraryVersion"

    // UI
    compile "com.android.support:recyclerview-v7:$rootProject.supportLibraryVersion"
    compile "com.android.support:cardview-v7:$rootProject.supportLibraryVersion"
    compile "com.android.support:design:$rootProject.supportLibraryVersion"
    compile "com.sloydev:collapsingavatartoolbar:$rootProject.collapsingAvatarToolbarVersion"

    // Networking
    compile "com.squareup.retrofit2:retrofit:$rootProject.ext.retrofitVersion"
    compile "com.squareup.retrofit2:adapter-rxjava:$rootProject.ext.retrofitVersion"
    compile "com.google.code.gson:gson:$rootProject.ext.gsonVersion"
    compile "com.squareup.retrofit2:converter-gson:$rootProject.ext.retrofitVersion"
    compile "com.squareup.okhttp3:okhttp:$rootProject.ext.okHttpVersion"
    compile "com.squareup.okhttp3:logging-interceptor:$rootProject.ext.okHttpVersion"
    compile "com.squareup.picasso:picasso:$rootProject.ext.picassoVersion"

    // ReactiveX
    compile "io.reactivex:rxjava:$rootProject.ext.rxJavaVersion"
    compile "com.artemzin.rxjava:proguard-rules:$rootProject.ext.rxJavaProGuardRulesVersion"
    compile "io.reactivex:rxandroid:$rootProject.ext.rxAndroidVersion"
    compile "com.jakewharton.rxbinding:rxbinding:$rootProject.ext.rxBindingVersion"
    compile "com.jakewharton.rxbinding:rxbinding-appcompat-v7:$rootProject.ext.rxBindingVersion"
    compile "com.jakewharton.rxbinding:rxbinding-support-v4:$rootProject.ext.rxBindingVersion"

    // Dependency Injection
    compile "com.google.dagger:dagger:$rootProject.ext.daggerVersion"
    apt "com.google.guava:guava:$rootProject.ext.guavaVersion"
    apt "com.google.dagger:dagger-compiler:$rootProject.ext.daggerVersion"
    provided "javax.annotation:jsr250-api:$rootProject.ext.jsr250Version"

    // Analytics
    compile("com.crashlytics.sdk.android:crashlytics:$rootProject.ext.crashlyticsVersion@aar") {
        transitive = true;
    }

    // Miscellaneous
    compile "org.greenrobot:eventbus:$rootProject.ext.eventBusVersion"
    compile "frankiesardo:icepick:$rootProject.ext.icepickVersion"
    provided "frankiesardo:icepick-processor:$rootProject.ext.icepickVersion"

    // Unit Testing
    testCompile "junit:junit:$rootProject.ext.jUnitVersion"
    testCompile "org.mockito:mockito-core:$rootProject.ext.mockitoVersion"
    testCompile("org.robolectric:robolectric:$rootProject.ext.robolectricVersion") {
        exclude group: 'javax.inject'
    }
    testCompile "org.hamcrest:hamcrest-all:$rootProject.ext.hamcrestVersion"
    testCompile "org.powermock:powermock-api-mockito:$rootProject.ext.powerMockVersion"
    testCompile "org.powermock:powermock-module-junit4:$rootProject.ext.powerMockVersion"
    testApt "com.google.dagger:dagger-compiler:$rootProject.ext.daggerVersion"

    // Instrumentation Testing
    androidTestCompile("com.android.support.test.espresso:espresso-core:$rootProject.ext.espressoVersion") {
        exclude group: 'javax.inject'
    }
    androidTestCompile "com.android.support.test.espresso:espresso-contrib:$rootProject.ext.espressoVersion"
    androidTestCompile "com.android.support.test.espresso:espresso-intents:$rootProject.ext.espressoVersion"
    androidTestCompile 'com.android.support.test:runner:0.5'

    // For resolving conflicts between main and test APK
    androidTestCompile "com.android.support:support-annotations:$rootProject.supportLibraryVersion"
    androidTestCompile "com.android.support:support-v4:$rootProject.supportLibraryVersion"
    androidTestCompile "com.android.support:appcompat-v7:$rootProject.supportLibraryVersion"
    androidTestCompile "com.android.support:design:$rootProject.supportLibraryVersion"
    androidTestCompile "com.android.support:recyclerview-v7:$rootProject.supportLibraryVersion"
}
