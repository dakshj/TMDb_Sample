apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'me.tatarka.retrolambda'

android {
    compileSdkVersion 24
    buildToolsVersion "23.0.3"

    defaultConfig {
        applicationId "com.daksh.tmdbsample"
        minSdkVersion 16
        targetSdkVersion 24
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    dexOptions {
        // Skip pre-dexing when running on Travis CI or when disabled via -Dpre-dex=false.
        preDexLibraries = preDexEnabled && !travisBuild
    }

    packagingOptions {
        exclude 'LICENSE.txt'
    }

    lintOptions {
        abortOnError false
    }

    dataBinding {
        enabled = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    signingConfigs {
        debug {
            storeFile file("../debug.jks")
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }
        debug {
            signingConfig signingConfigs.debug
        }
    }

    productFlavors {
        Production_ {
            buildConfigField "boolean", "IS_PRODUCTION", "true"
            buildConfigField 'String', 'BASE_URL', '"http://api.themoviedb.org/3/"'
            buildConfigField 'String', 'API_KEY', '"3f41e7a0addc26031fdb06aa1c4b6bc0"'
        }

        Development_ {
            buildConfigField "boolean", "IS_PRODUCTION", "false"
            buildConfigField 'String', 'BASE_URL', '"http://api.themoviedb.org/3/"'
            buildConfigField 'String', 'API_KEY', '"3f41e7a0addc26031fdb06aa1c4b6bc0"'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    //Support
    compile 'com.android.support:support-v4:24.2.0'
    compile 'com.android.support:appcompat-v7:24.2.0'

    //UI
    compile 'com.android.support:recyclerview-v7:24.2.0'
    compile 'com.android.support:design:24.2.0'

    //Networking
    compile 'com.squareup.retrofit2:retrofit:2.1.0'
    compile 'com.squareup.retrofit2:adapter-rxjava:2.1.0'
    compile 'com.google.code.gson:gson:2.7'
    compile 'com.squareup.retrofit2:converter-gson:2.1.0'
    compile 'com.squareup.okhttp3:okhttp:3.4.1'
    compile 'com.squareup.okhttp3:logging-interceptor:3.4.1'
    compile 'com.squareup.picasso:picasso:2.5.2'

    //ReactiveX
    compile 'io.reactivex:rxjava:1.1.9'
    compile 'io.reactivex:rxandroid:1.2.1'

    //Dependency Injection
    compile 'com.google.dagger:dagger:2.6.1'
    apt 'com.google.guava:guava:19.0'
    apt 'com.google.dagger:dagger-compiler:2.6.1'
    provided 'javax.annotation:jsr250-api:1.0'

    //Miscellaneous
    compile 'org.greenrobot:eventbus:3.0.0'

    //Unit Testing
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.10.19'
    testCompile('org.robolectric:robolectric:3.1.2') {
        exclude group: 'javax.inject'
    }
    testCompile 'org.hamcrest:hamcrest-all:1.3'
    testCompile 'org.powermock:powermock-api-mockito:1.6.5'
    testCompile 'org.powermock:powermock-module-junit4:1.6.5'
    testApt 'com.google.dagger:dagger-compiler:2.6.1'

    //Instrumentation Testing
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2') {
        exclude group: 'javax.inject'
    }
    androidTestCompile 'com.android.support.test.espresso:espresso-contrib:2.2.2'
    androidTestCompile 'com.android.support.test:runner:0.5'

    //To resolve conflicts between main and test APK
    androidTestCompile 'com.android.support:support-annotations:24.2.0'
    androidTestCompile 'com.android.support:support-v4:24.2.0'
    androidTestCompile 'com.android.support:appcompat-v7:24.2.0'
    androidTestCompile 'com.android.support:design:24.2.0'
    androidTestCompile 'com.android.support:recyclerview-v7:24.2.0'
}
